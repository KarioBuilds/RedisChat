plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'maven-publish'
    id 'java'
}


group = 'dev.unnm3d'
version = '4.5.1'


repositories {
    mavenLocal()
    maven { url 'https://repo.codemc.org/repository/maven-public/' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url 'https://repo.xenondevs.xyz/releases' }
    maven { url 'https://repo.maven.apache.org/maven2/' }

}

dependencies {
    implementation 'net.kyori:adventure-text-minimessage:4.16.0'
    implementation 'net.kyori:adventure-text-serializer-plain:4.16.0'
    implementation 'net.kyori:adventure-platform-bukkit:4.3.2'

    implementation 'com.github.Exlll.ConfigLib:configlib-paper:v4.5.0'
    implementation 'xyz.xenondevs.invui:invui-core:1.24'
    implementation 'xyz.xenondevs.invui:inventory-access-r7:1.24'
    implementation 'xyz.xenondevs.invui:inventory-access-r9:1.24'
    implementation 'xyz.xenondevs.invui:inventory-access-r11:1.24'
    implementation 'xyz.xenondevs.invui:inventory-access-r14:1.24'
    implementation 'xyz.xenondevs.invui:inventory-access-r15:1.24'
    implementation 'xyz.xenondevs.invui:inventory-access-r16:1.24'
    implementation 'xyz.xenondevs.invui:inventory-access-r17:1.24'
    implementation 'xyz.xenondevs.invui:inventory-access-r18:1.24'
    implementation 'dev.jorel:commandapi-bukkit-shade:9.3.0'
    implementation 'com.github.Anon8281:UniversalScheduler:0.1.6'

    compileOnly 'io.papermc.paper:paper-api:1.17.1-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.3'
    compileOnly 'io.lettuce:lettuce-core:6.2.5.RELEASE'
    compileOnly 'org.jetbrains:annotations:24.0.1'
    compileOnly 'com.zaxxer:HikariCP:5.0.1'
    compileOnly 'org.mariadb.jdbc:mariadb-java-client:3.1.4'
    compileOnly 'com.h2database:h2:2.2.220'
    compileOnly 'net.luckperms:api:5.4'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7'
    compileOnly 'com.github.oraxen:oraxen:1.162.0'
    compileOnly 'com.github.LeonMangler:PremiumVanishAPI:2.8.8'
    compileOnly 'org.projectlombok:lombok:1.18.26'
    compileOnly 'com.github.Spicord.Spicord:spicord-common:v5-SNAPSHOT'
    compileOnly 'net.dv8tion:JDA:5.0.0-beta.16'
    compileOnly 'org.apache.commons:commons-collections4:4.4'


    annotationProcessor('org.projectlombok:lombok:1.18.26')
}

shadowJar {
    destinationDirectory.set(file("$rootDir/target"))
    archiveClassifier.set('')
    dependencies {
        include dependency('net.kyori:adventure-api')
        include dependency('net.kyori:adventure-key')
        include dependency('net.kyori:adventure-nbt')
        include dependency('net.kyori:adventure-platform-api')
        include dependency('net.kyori:adventure-platform-bukkit')
        include dependency('net.kyori:adventure-platform-facet')
        include dependency('net.kyori:adventure-text-minimessage')
        include dependency('net.kyori:adventure-text-serializer-plain')
        include dependency('net.kyori:adventure-text-serializer-legacy')
        include dependency('net.kyori:adventure-text-serializer-gson')
        include dependency('net.kyori:adventure-text-serializer-json')
        include dependency('net.kyori:adventure-text-serializer-gson-legacy-impl')
        include dependency('net.kyori:adventure-text-serializer-bungeecord')
        include dependency('net.kyori:examination-api')
        include dependency('net.kyori:option')

        include dependency('com.github.Exlll.ConfigLib:configlib-paper')
        include dependency('com.github.Exlll.ConfigLib:configlib-yaml')
        include dependency("com.github.Exlll.ConfigLib:configlib-core")

        include dependency('xyz.xenondevs.invui:invui-core')
        include dependency('xyz.xenondevs.invui:inventory-access')
        include dependency('xyz.xenondevs.invui:inventory-access-r7')
        include dependency('xyz.xenondevs.invui:inventory-access-r9')
        include dependency('xyz.xenondevs.invui:inventory-access-r11')
        include dependency('xyz.xenondevs.invui:inventory-access-r14')
        include dependency('xyz.xenondevs.invui:inventory-access-r15')
        include dependency('xyz.xenondevs.invui:inventory-access-r16')
        include dependency('xyz.xenondevs.invui:inventory-access-r17')
        include dependency('xyz.xenondevs.invui:inventory-access-r18')
        include dependency('dev.jorel:commandapi-bukkit-shade:9.3.0')
        include dependency('com.github.Anon8281:UniversalScheduler:0.1.6')
    }

    exclude 'colors.bin'
    exclude 'net.kyori.adventure.title'
    exclude 'net.kyori.adventure.bossbar'
    exclude 'net.kyori.adventure.translation'
    relocate 'net.kyori.adventure', 'dev.unnm3d.redischat.libraries.adventure'
    relocate 'net.kyori.option', 'dev.unnm3d.redischat.libraries.adventure.option'
    relocate 'net.kyori.examination', 'dev.unnm3d.redischat.libraries.adventure.examination'

    relocate 'de.exlll.configlib', 'dev.unnm3d.redischat.libraries.configlib'
    relocate 'xyz.xenondevs.invui', 'dev.unnm3d.redischat.libraries.invui'
    relocate 'xyz.xenondevs.inventoryaccess', 'dev.unnm3d.redischat.libraries.inventoryaccess'
    relocate 'dev.jorel.commandapi', 'dev.unnm3d.redischat.libraries.commandapi'
    relocate 'com.github.Anon8281.universalScheduler', 'dev.unnm3d.redischat.libraries.universalScheduler'

}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }

    withSourcesJar()
    //withJavadocJar()

}

tasks.withType(JavaCompile).configureEach {
    options.release.set targetJavaVersion
    options.encoding = 'UTF-8'
}


processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'dev.unnm3d'
            artifactId = 'redischat'
            version = "$version"
            artifact shadowJar
            //artifact javadocJar
            artifact sourcesJar
        }
    }
}

jar.dependsOn shadowJar
clean.delete "$rootDir/target"